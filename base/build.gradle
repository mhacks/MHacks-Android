apply plugin: 'com.android.feature'

apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

ext {
    bugsnag_key = ext.MHACKS_BUGSNAG_KEY
    google_maps_api_key = ext.MHACKS_GOOGLE_MAPS_API_KEY
    gcm_server_id = ext.GCM_SERVER_ID
}

android {
    compileSdkVersion 28
    buildToolsVersion "27.0.3"
    baseFeature true
    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("mhacks-release-key.keystore")
            keyAlias "mykey"

            def console = System.console()

            if (console != null) {
                def password = new String(console.readPassword("\n> Enter keystore password: "))
                storePassword password
                keyPassword password
            }
        }
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionName "10.0.0"
        resValue "string", "google_maps_api_key", google_maps_api_key
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            multiDexEnabled true
            resValue "string", "google_maps_api_key", google_maps_api_key
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            resValue "string", "google_maps_api_key", google_maps_api_key
        }
    }
    productFlavors {

    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlin_version"

    // Play Services
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.firebase:firebase-messaging:17.3.1'

    implementation "com.android.support:appcompat-v7:$rootProject.google_play_version"
    implementation "com.android.support:support-v4:$rootProject.google_play_version"
    implementation "com.android.support:support-media-compat:$rootProject.google_play_version"

    // Networking
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'

    // Persistence
    implementation "android.arch.persistence.room:runtime:$rootProject.room_version"
    implementation "android.arch.persistence.room:rxjava2:$rootProject.room_version"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    // Debugging
    implementation 'com.jakewharton.timber:timber:4.7.0'

    kapt "android.arch.persistence.room:compiler:$room_version"

    feature project(':ui')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: "com.google.gms.google-services"

repositories {
    mavenCentral()
}
