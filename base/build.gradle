//apply plugin: "com.android.application"
apply plugin: 'com.android.feature'
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

ext {
    bugsnag_key = ext.MHACKS_BUGSNAG_KEY
    google_maps_api_key = ext.MHACKS_GOOGLE_MAPS_API_KEY
    gcm_server_id = ext.GCM_SERVER_ID
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    baseFeature true
    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("mhacks-release-key.keystore")
            keyAlias "mykey"

            def console = System.console()

            if (console != null) {
                def password = new String(console.readPassword("\n> Enter keystore password: "))
                storePassword password
                keyPassword password
            }
        }
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionName "10.0.0"
        resValue "string", "google_maps_api_key", google_maps_api_key
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            multiDexEnabled true
            resValue "string", "google_maps_api_key", google_maps_api_key
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            resValue "string", "google_maps_api_key", google_maps_api_key
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Play Services
    implementation 'com.google.android.gms:play-services-gcm:11.8.0'
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation 'com.google.android.gms:play-services-vision:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'

    // Support libraries
    implementation "com.android.support:appcompat-v7:$google_play_version"
    implementation "com.android.support:support-v4:$google_play_version"
    implementation "com.android.support:cardview-v7:$google_play_version"
    implementation "com.android.support:recyclerview-v7:$google_play_version"
    implementation "com.android.support:design:$google_play_version"

    // View
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.kenglxn.QRGen:android:2.2.0'
    implementation 'com.github.vipulasri:timelineview:1.0.5'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    // Persistence
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // Debugging
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.jakewharton.timber:timber:4.6.0'

    // Dagger related libraries
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Other
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    implementation("com.github.hotchemi:permissionsdispatcher:3.0.1") {
        exclude module: "support-v13"
    }
    kapt "android.arch.persistence.room:compiler:$room_version"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:3.0.1"

    feature project(':ui')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: "com.google.gms.google-services"

repositories {
    mavenCentral()
}
