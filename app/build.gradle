apply plugin: 'com.android.application'

apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

ext {
    google_maps_api_key = ext.MHACKS_GOOGLE_MAPS_API_KEY
    gcm_server_id = ext.GCM_SERVER_ID
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    dynamicFeatures = [":events"]

    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("mhacks-release-key.keystore")
            keyAlias "mykey"

            def console = System.console()

            if (console != null) {
                def password = new String(console.readPassword("\n> Enter keystore password: "))
                storePassword password
                keyPassword password
            }
        }
    }
    defaultConfig {
        applicationId 'org.mhacks.app'

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionName "11.0.0"

        buildConfigField "String", "FIXED_START_DATE", "\"2018-10-12T00:00:00\""
        buildConfigField "String", "INSTANT_APP_URL", "\"schedule\""

        buildConfigField "String", "WIFI_SSID", "\"MHacks11\""
        buildConfigField "String", "WIFI_PASSWORD", "\"anyonecancode\""

        buildConfigField "String", "MHACKS_EMAIL", "\"hackathon@umich.edu\""

        buildConfigField "String",
                "SLACK_INVITE_URL",
                "\"https://join.slack.com/t/mhacks11/shared_invite/enQtNDQzMDI0MDY3ODQ1LTM3YTIyMmJmNjU3NGM2NDk1MzQzZjZmMmY3ZDliZmZlNTRjOGNiMTc2OWRiZjIyODBiNzk2ZGQ4YjBmMjlkZjU\""

        buildConfigField 'double', 'LAT', '42.2694'
        buildConfigField 'double', 'LNG', '-83.7425'

        resValue "string", "google_maps_api_key", google_maps_api_key

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            resValue "string", "google_maps_api_key", google_maps_api_key
            signingConfig signingConfigs.release

            buildConfigField "String", "API_URL", "\"https://mhacks.org/v1/\""
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            resValue "string", "google_maps_api_key", google_maps_api_key
            buildConfigField "String", "API_URL", "\"https://staging.mhacks.org/v1/\""
        }
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.kotlin

    // Play Services
    implementation 'com.google.firebase:firebase-core:17.0.1'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'

    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.0.1'

    // Networking
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.1'

    // Persistence
    implementation 'androidx.room:room-runtime:2.1.0'
    implementation 'androidx.room:room-rxjava2:2.1.0'
    kapt 'androidx.room:room-compiler:2.1.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'

    // Debugging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation project(':core')

    implementation deps.google.material

    implementation deps.androidx.constraintlayout
    implementation deps.androidx.navigation
    implementation deps.androidx.navigationKtx

    implementation deps.dagger.dagger
    implementation deps.dagger.daggerAndroid
    kapt deps.dagger.daggerCompiler
    kapt deps.dagger.daggerAndroidProcessor
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: "com.google.gms.google-services"

repositories {
    mavenCentral()
}
