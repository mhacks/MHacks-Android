apply plugin: 'com.android.application'

apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

apply from: '../gradle/shared.gradle'

ext {
    google_maps_api_key = ext.MHACKS_GOOGLE_MAPS_API_KEY
    gcm_server_id = ext.GCM_SERVER_ID
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    dynamicFeatures = [":events", ":maps", ":signin", ":ticket", ":qrscan", ":postannouncement", ":announcements", ":pref", ":welcome", ":info"]

    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("mhacks-release-key.keystore")
            keyAlias "mykey"

            def console = System.console()

            if (console != null) {
                def password = new String(console.readPassword("\n> Enter keystore password: "))
                storePassword password
                keyPassword password
            }
        }
    }
    defaultConfig {
        applicationId 'org.mhacks.app'

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionName "11.0.0"

        buildConfigField "String", "FIXED_START_DATE", "\"2019-10-11T00:00:00\""
        buildConfigField "String", "INSTANT_APP_URL", "\"schedule\""

        buildConfigField "String", "WIFI_SSID", "\"MHacks12\""
        buildConfigField "String", "WIFI_PASSWORD", "\"anyonecancode\""

        buildConfigField "String", "MHACKS_EMAIL", "\"hackathon@umich.edu\""

        buildConfigField "String",
                "SLACK_INVITE_URL",
                "\"https://join.slack.com/t/mhacks12/shared_invite/enQtNzM5MDEwMjUxNzQ0LTM3NWQ0ZDFmYWEyYmMyY2UyMTgxY2VmNjg1M2I4MTQwNzkxZWIyOGZmYjk2MDJjNDc4YjZiY2M4ZTMwYzg1MWQ\""

        resValue "string", "google_maps_api_key", google_maps_api_key

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            resValue "string", "google_maps_api_key", google_maps_api_key
            signingConfig signingConfigs.release

            buildConfigField "String", "API_URL", "\"https://mhacks.org/v1/\""
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            resValue "string", "google_maps_api_key", google_maps_api_key
            buildConfigField "String", "API_URL", "\"https://staging.mhacks.org/v1/\""
        }
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':core')
    implementation project(':eventlibrary')

    implementation 'com.google.android.material:material:1.1.0-alpha10'

    // Play Services
    implementation 'com.google.firebase:firebase-core:17.1.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.1.0'

    api project(':ratingmanager')

    // Networking
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.1'

    implementation deps.google.material

    implementation deps.androidx.navigation
    implementation deps.androidx.navigationKtx
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: "com.google.gms.google-services"

repositories {
    mavenCentral()
}
